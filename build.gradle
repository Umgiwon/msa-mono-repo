plugins {
    id 'java' // 도구 사용을 위해 루트에 적용(하위 프로젝트도 필요에 따라 Java를 적용함)
    id 'org.springframework.boot' version "${springBootVersion}" apply false
    id 'io.spring.dependency-management' version "${dependencyManagementVersion}" apply false
    id 'com.github.ben-manes.versions' version '0.46.0' apply false
}

group = 'com.msa'
version = '0.0.1-SNAPSHOT'
description = 'monorepo'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(Integer.parseInt(property('javaVersion') as String))
    }
}

repositories {
    mavenCentral()
}

// 프로젝트 전반에서 사용할 수 있는 추가 속성 정의
ext {
    mapstructVersion = property('mapstructVersion')
    jjwtVersion = property('jjwtVersion')
    p6spyVersion = property('p6spyVersion')
    querydslVersion = property('querydslVersion')
    springdocVersion = property('springdocVersion')
    lombokVersion = property('lombokVersion')
}

// 하위 프로젝트에 공통으로 적용할 설정 정의
subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    group = rootProject.group
    version = rootProject.version

    repositories {
        mavenCentral()
    }

    // 공통으로 사용할 compileOnly/annotationProcessor (Lombok 관련)
    dependencies {
        compileOnly libs.lombok
        annotationProcessor libs.lombok
    }

    // 컴파일 시점의 의존성 구성을 정의
    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    // Spring Boot BOM을 dependencyManagement로 import (버전 일관성 유지)
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${property('springBootVersion')}"
        }
    }

//    tasks.withType(Test) {
//        useJUnitPlatform()
//    }

    // 실행 시 현재 모듈의 이름과 그룹, 버전을 출력
    tasks.register('printModule') {
        doLast {
            println "Module: ${project.name} (${project.group}:${project.version})"
        }
    }
}

// 편의: 전체 빌드 task 정의
task buildAll {
    description = "Build all subprojects"
    group = "build"
    dependsOn subprojects.collect { it.tasks.matching { t -> t.name == 'build' } }
}
